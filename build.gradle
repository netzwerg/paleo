/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'me.champeau.gradle:jmh-gradle-plugin:0.4.4'
    }
}

plugins {
    id 'ch.netzwerg.release' version '1.2.5'
    id 'com.jfrog.bintray' version '1.8.4'
}

apply from: 'gradle/dependencies.gradle'

allprojects {

    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'idea'

    group = 'ch.netzwerg'
    version = rootProject.version

    bintray {
        dryRun = project.hasProperty('dryRun') && project.dryRun.toBoolean()
        if (project.hasProperty('BINTRAY_USER')) {
            user = "$BINTRAY_USER"
        }
        if (project.hasProperty('BINTRAY_API_KEY')) {
            key = "$BINTRAY_API_KEY"
        }
        configurations = ['published', 'archives']
        pkg {
            repo = 'maven'
            name = 'paleo'
            licenses = ['Apache-2.0']
            desc = 'Immutable Java 8 data frames with typed columns'
            websiteUrl = 'https://github.com/netzwerg/paleo'
            issueTrackerUrl = 'https://github.com/netzwerg/paleo/issues'
            vcsUrl = 'https://github.com/netzwerg/paleo.git'
            version {
                name = project.version
                vcsTag = project.release.tagName
            }
        }
    }

    idea.module {
        inheritOutputDirs = true
    }

}

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'me.champeau.gradle.jmh'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.encoding = 'UTF-8'
    javadoc.options.encoding = 'UTF-8'

    repositories {
        jcenter()
    }

    dependencies {
        testCompile libraries.junit
        testCompile libraries.chabis
    }

    configurations {
        published
    }

    task copyLicense(type: Copy) {
        from "$rootDir"
        into 'build/resources/main/META-INF'
        include 'LICENSE'
    }

    jar {
        dependsOn copyLicense
        doFirst {
            manifest {
                attributes(
                        'Build-Time': new Date().format("EEE, dd MMM yyyy HH:mm:ss z"),
                        'Implementation-Version': rootProject.version,
                        'Bundle-License': 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                )
            }
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        published sourceJar
    }

    idea.module {
        scopes.PROVIDED.plus += [ configurations.jmh ]
    }

    jmh {
        jmhVersion = versions.jmhVersion
    }

}

configurations {
    published
}

release {
    dependsOn subprojects.build
    push = false
}
